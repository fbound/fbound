ext.ossrhUsername = ossrhUsername
ext.ossrhPassword = ossrhPassword

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    version = '0.1.0-SNAPSHOT'
    group = 'org.fbound'

    buildscript {
        repositories {
            mavenCentral()
        }
    }

    repositories {
        mavenCentral()
    }

    test {
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    jar {
        manifest {
            attributes(
                "License-Name": "APACHE-2.0",
                "License-URL": "http://www.apache.org/licenses/LICENSE-2.0"
            )
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                from components.java
                pom {
                    name = project.name
                    description = 'fbound - tools for building better APIs'
                    url = 'https://github.com/fbound/fbound'
                    licenses {
                        license {
                            name = 'APACHE-2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'mstevenson'
                            name = 'Matt Stevenson'
                            email = 'matt@fbound.org'
                        }
                    }
                    scm {
                        url = 'https://github.com/fbound/fbound'
                        connection = 'scm:git:git://github.com/fbound/fbound.git'
                        developerConnection = 'scm:git:ssh://git@github.com/fbound/fbound.git'
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
